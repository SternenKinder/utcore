# HOW TO BUILD (A) LIBRAR-Y/IES FROM A MODULE
# Building libraries from modules should be consistent for each module.
# Use the following scheme for each new library created:
#
# a) Check for required libraries. If they are not available return False and export flags if needed
# b) Define the src-subdirectories for which the library should be compiled
#    and glob all files in there
# c) Define a [LIBRARY]_options variable for the library containing all dependencies
#    from other libraries. Create a clone from the master environment and add those options.
# d) WINDOWS ONLY: Create a header file which implements dll interface functionality
#    dependent from a [LIBRARY]_DLL define. If the define is set, the dll should be exported,
#    if not, the dll should be imported. Set the define in the preprocessor options for the environment.
# e) Build the library!
# f) Remove all entries for (at least) the LIBS and LIBPATH keys in the options and
#    add only the newly-compiled library as a dependcy. This will avoid multiple includes
#    of libraries. Also verify that the import/export-switch define is not set.
# g) Export the options so that other libraries can base on this library. 
# h) Optionally setup help and ide projects
#
# The use of options and possibility to export them makes hierarchical build environments
# obsolete. Avoid exporting new environments to the build system.

import sys
import os.path

have_boost = False
have_tinyxml = False
have_log4cpp = False

Import ( '*' )

# a)
if not( have_boost and have_tinyxml and have_log4cpp):
	print "Boost, tinyxml or log4cpp missing -- not building core libraries"
	have_utcore = False
	utcore_options = {}
	utcore_all_options = {}
	Export( 'utcore_options', 'have_utcore', 'utcore_all_options' )
	Return ('have_utcore')
	
# b)
subdirs = [  'utMath', 'utMeasurement', 'utCalibration', 'utTracking', 'utUtil' ]
headers = globSourceFiles(  '*.h' )
sources = globSourceFiles(  '*.cpp' )
for dir in subdirs:	
	headers += globSourceFiles( dir + '/*.h' )
	sources += globSourceFiles( dir + '/*.cpp' )


# c)		
utcore_options = mergeOptions( boost_options, tinyxml_options, log4cpp_options, lapack_options)
utcore_options ['CPPPATH'] += [ getCurrentPath() ]

env = masterEnv.Clone()
env.AppendUnique( **utcore_options )

# d)
if sys.platform == 'win32':	
	env.Append( CPPDEFINES = 'UBITRACK_DLL' )


# e)	
# compile all source files into single library
# {buildenvironment, source files, name of the library, build target}
libs = setupLibraryBuild(env, sources, 'utCore', 'core')

# f)
utcore_all_options = utcore_options.copy()
utcore_options[ 'LIBS' ] = []
utcore_options[ 'LIBPATH' ] = []
utcore_options[ 'LIBS' ] += libs
utcore_options[ 'LIBPATH' ] += [ install_library_prefix ]
utcore_all_options[ 'LIBS' ] += libs
utcore_all_options[ 'LIBPATH' ] += [ install_library_prefix ]

# g)
have_utcore = libs != 0
Export( 'have_utcore', 'utcore_options', 'utcore_all_options' )

# h)
generateHelp(utcore_options)
createVisualStudioProject(env, sources, headers, 'utCore')
